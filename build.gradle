import org.gradle.api.artifacts.maven.MavenDeployment

allprojects {
    apply {
        plugin 'java'
        plugin 'groovy'
        plugin 'eclipse'
        plugin 'idea'
    }

    repositories {
        mavenCentral()
    }
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

apply {
    plugin 'maven'
    plugin 'signing'
    plugin 'codenarc'
}

group = 'com.apphance.flow'
ext.artifactId = 'apphance-flow'

task sourcesJar(type: Jar, dependsOn: classes, description: 'Prepares .jar file containing the source code of flow') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc, description: 'Prepares .jar file with documentation generated out of the code') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

artifacts {
    if (isReleaseVersion) {
        archives sourcesJar
        archives javadocJar
        archives jar
    }
}

codenarc {
    configFile = file('codenarc.groovy')
}

signing {
    if (isReleaseVersion) {
        sign configurations.archives
    }
}

def uploadUsername = project.hasProperty('sonatypeUsername') ? project['sonatypeUsername'] : ''
def uploadPassword = project.hasProperty('sonatypePassword') ? project['sonatypePassword'] : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                if (isReleaseVersion) {
                    signing.signPom(deployment)
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: uploadUsername, password: uploadPassword)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: uploadUsername, password: uploadPassword)
            }

            pom.artifactId = artifactId

            pom.project {
                name 'Apphance Flow'
                packaging 'jar'
                description 'Apphance Flow - gradle plugin to automate builds for mobile applications'
                url 'http://flow.apphance.com/'

                scm {
                    url 'scm:git@github.com:apphance/Apphance-MobilE-Build-Automation.git'
                    connection 'scm:git@github.com:apphance/Apphance-MobilE-Build-Automation.git'
                    developerConnection 'scm:git@github.com:apphance/Apphance-MobilE-Build-Automation.git'
                }

                licenses {
                    license {
                        name 'NewBSD Licence'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jarek.potiuk.apphance'
                        name 'Jarek Potiuk'
                    }
                    developer {
                        id 'blazej.marcinkiewicz'
                        name 'Blazej Marcinkiewicz'
                    }
                    developer {
                        id 'wojtek.erbetowski'
                        name 'Wojtek Erbetowski'
                    }
                    developer {
                        id 'maciej.opala'
                        name 'Maciej Opala'
                    }
                    developer {
                        id 'arkadiusz.konior'
                        name 'Arkadiusz Konior'
                    }
                }
            }

        }
    }
}

configure(install.repositories.mavenInstaller) {
    pom.project { artifactId artifactId }
}

sourceSets {
    testUnit.groovy.srcDirs = ['src/test/groovy/unit']
    testIntegration.groovy.srcDirs = ['src/test/groovy/integration']
    testFunctional.groovy.srcDirs = ['src/test/groovy/functional']
}

dependencies {

    compile gradleApi()
    groovy localGroovy()

    compile('com.google.inject:guice:3.0') {
        exclude group: 'org.sonatype.sisu.inject'
    }
    compile('com.google.inject.extensions:guice-multibindings:3.0') {
        exclude group: 'org.sonatype.sisu.inject'
    }
    compile('com.google.inject.extensions:guice-assistedinject:3.0') {
        exclude group: 'org.sonatype.sisu.inject'
    }
    compile('org.apache.httpcomponents:httpmime:4.2.2') {
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    compile 'org.imgscalr:imgscalr-lib:4.2'

    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'com.google.guava:guava:14.0.1'
    compile 'com.thoughtworks.qdox:qdox:1.12.1'
    compile('com.google.android:android:4.1.1.4') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testRuntime 'cglib:cglib-nodep:2.2.2'
    compile fileTree(dir: 'libs', include: '*.jar')

    testUnitCompile sourceSets.main.output
    testUnitCompile configurations.testCompile
    testUnitCompile sourceSets.test.output
    testUnitRuntime configurations.testRuntime

    testIntegrationCompile sourceSets.main.output
    testIntegrationCompile configurations.testCompile
    testIntegrationCompile sourceSets.test.output
    testIntegrationRuntime configurations.testRuntime

    testFunctionalCompile sourceSets.main.output
    testFunctionalCompile configurations.testCompile
    testFunctionalCompile sourceSets.test.output
    testFunctionalRuntime configurations.testRuntime
}

def beforeTestClosure = { descriptor ->
    logger.lifecycle("Test -> class: ${descriptor.className}, name: '${descriptor.name}'")
}

task testUnit(type: Test) {
    testClassesDir = sourceSets.testUnit.output.classesDir
    classpath = sourceSets.testUnit.runtimeClasspath
    beforeTest(beforeTestClosure)
}

task testIntegration(type: Test) {
    testClassesDir = sourceSets.testIntegration.output.classesDir
    classpath = sourceSets.testIntegration.runtimeClasspath
    beforeTest(beforeTestClosure)
}

task testFunctional(type: Test) {
    testClassesDir = sourceSets.testFunctional.output.classesDir
    classpath = sourceSets.testFunctional.runtimeClasspath
    beforeTest(beforeTestClosure)
}

test {
    beforeTest(beforeTestClosure)
}


idea {
    module {
        inheritOutputDirs = false

        outputDir = file('build/classes/main')
        testOutputDir = file('build/classes/test')

        testSourceDirs = [
                file('src/test/groovy/unit'),
                file('src/test/groovy/integration'),
                file('src/test/groovy/functional'),
        ]
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

file('testProjects').traverse(nameFilter: ~/flow-(log|ota|tmp)/) { File dir ->
    tasks['clean'] << {
        ant.delete(dir: dir.absolutePath)
    }
}

jar {
    from file('libs').listFiles().collect { zipTree(it) }
}

task fatJar(type: Jar, dependsOn: jar) {
    appendix = "standalone"
    from zipTree(jar.archivePath)
    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.name.endsWith('RSA') || it.name.endsWith('SF') || it.name.endsWith('DSA') }
        }
    }
    manifest.attributes("Main-Class": "com.apphance.flow.plugins.android.apphance.Main")
}
